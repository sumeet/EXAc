#define CC_LENGTH 16

xa {
  link 800, 802, 799
  fopen(199) {
    loop {
      i/x = 0
      while (i/x < 16) {
        $ch = fread()
        i/x += 1
      }
      $ch = -1

      // HAX: read out the file twice so once validated,
      // the reader exa can write it back out
      file.seek(-16)
      i/x = 0
      while (i/x < 16) {
        $ch = fread()
        i/x += 1
      }
      $ch = -1

      file.seek(-15)
    }
  }
}

xb {
  loop {
    fcreate() {
      digit/x = $ch
      while (digit/x != -1) {
        fwrite(digit/x)
        digit/x = $ch
      }
      
      // validation step
      file.seek(-9999)

      // multiply odds by 2
      c/x = 0
      while (c/x < 8) {
        digit/t = fread()
        file.seek(-1)
        fwrite(digit/t * 2)
        file.seek(1)

        c/x += 1
      }

      file.seek(-9999)
      // subtract 9 if > 9
      while (!feof) {
        digit/x = fread()
        if (digit/x > 9) {
          file.seek(-1)
          fwrite(digit/x - 9)
        }
      }
      
      // now sum it all up
      file.seek(-9999)
      sum/x = 0
      while (!feof) {
        sum/x += fread()
      }
      if (sum/x < 0) {
        // there was garbage in here, let's bail
        file.wipe()
        while ($ch != -1) {}
        continue
      }
      last_digit/x = sum/x ~ 1
      if (last_digit/x == 0) {
        file.seek(-9999)
        digit/x = $ch
        while (digit/x != -1) {
          fwrite(digit/x)
          digit/x = $ch
        }
        spawn {
          link 800, 802, 799
          kill
          HALT
        }
        HALT
      } else {
        file.wipe()
        while ($ch != -1) {}
        continue
      }
    }
  }
}
